group = "com.example.pl_image_downloader"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.9.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

def engineVersion = {
    def properties = new Properties()
    def localPropertiesFile = new File(rootProject.projectDir, "local.properties")
    if (localPropertiesFile.exists()) {
        localPropertiesFile.withReader("UTF-8") { reader ->
            properties.load(reader)
        }
    }
    def flutterSdkPath = properties.getProperty("flutter.sdk")
    if (flutterSdkPath == null) {
        flutterSdkPath = System.getenv("FLUTTER_ROOT")
    }
    if (flutterSdkPath != null) {
        def engineVersionFile = new File(flutterSdkPath, "bin/internal/engine.version")
        if (engineVersionFile.exists()) {
            return engineVersionFile.text.trim()
        }
    }
    return null
}()

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://storage.googleapis.com/download.flutter.io")
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"


android {
    namespace = "com.example.pl_image_downloader"

    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 24
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")

        implementation("androidx.core:core-ktx:1.17.0")
        implementation("androidx.appcompat:appcompat:1.7.1")
        implementation("androidx.work:work-runtime-ktx:2.11.0")

        if (engineVersion != null) {
            debugCompileOnly("io.flutter:flutter_embedding_debug:1.0.0-$engineVersion")
        }
    }
}
